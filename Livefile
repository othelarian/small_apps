# REQUIRES ############################

require! {
  bach, path, pug, sass
  'fs-extra': fse
  livescript: ls
  './core/config': { cfg }
}

# OPTIONS #############################

option \cmd,     \c, \String,  'command to use (with the `cmd` task)'
option \github,  \g, \Boolean, ''
option \id,      \p, \Int,     'project id'
option \release, \r, \Boolean, ''
option \watch,   \w, \Boolean, '(only for dev mode)'

# FUNCTIONS ###########################

build-all = !->
  end-all = (cb) !->
    cfg.id = 0
    cb void 5
  start-all = (cb) !->
    cfg.dir = '.'
    cfg.out = "./#{cfg.dest}"
    cfg.src = cfg.root
    cb void 4
  cfg.list = cfg.list.filter (.active), cfg.list
  args =
    [building cfg for _ til cfg.list.length].flat!
    |> ([start-all, create-dir, compile-src, end-all] ++)
  (bach.series args) final-cb

build-end = (cb) !->
  console.log "building [ #{cfg.list[cfg.id].name} ] DONE"
  cfg.id += 1
  cb void 22

build-start = (cb) !->
  prj = cfg.list[cfg.id]
  console.log "starting building [ #{prj.name} ] ..."
  cfg.dir = "./#{prj.path}"
  cfg.out = "./#{cfg.dest}/#{prj.path}"
  cfg.src = prj.src
  cb void 21

building = (cfg) ->
  copy-statiq-prep = (cb) !-> copy-statiq cb, yes
  pack = [build-start, create-dir, compile-src, build-end]
  if cfg.statiq? and cfg.statiq then pack.splice 2 0 copy-statiq-prep
  if cfg.font? and cfg.font then pack.splice (pack.length - 2), 0, get-font
  pack

check-env = (opts) !->
  [cfg.dest, cfg.release, cfg.github] = switch
    | opts.github  => [cfg.dest_path.github,   yes, yes]
    | opts.release => [cfg.dest_path.releaase, yes, no]
    | _            => [cfg.dest_path.debug,    no,  no]

compile-src = (cb) !->
  in-lst = (lg, fc) !->
    inf = "#{cfg.dir}/#{fc[0]}"
    outf = "#{cfg.out}/#{fc[1]}"
    do-exec inf, outf, lg
    if cfg.watching then cfg.chok[inf.substring 2] = { lg, outf }
  in-lg = (lg, lst) -> for fc in lst then in-lst lg, fc
  for lg, lst of cfg.src then in-lg lg, lst
  cb void 13

create-dir = (cb) !->>
  try
    await fse.mkdirs "./#{cfg.out}"
    cb void 0
  catch e
    if e.code is 'EEXIST' then cb void 1 else cb e void

copy-statiq = (cb, prep = no) !->>
  if await fse.pathExists "#{cfg.dir}/statiq"
    console.log 'copy statiq file'
    try
      await fse.mkdirs "#{cfg.out}/statiq"
      src_files = await fse.readdir "#{cfg.dir}/statiq"
      src_mod = [...src_files]
      for outf in await fse.readdir "#{cfg.out}/statiq"
        if outf not in src_files then fse.remove "#{cfg.out}/statiq/#outf"
        else
          odt = (await fse.stat "#{cfg.out}/statiq/#outf").mtimeMs
          sdt = (await fse.stat "#{cfg.dir}/statiq/#outf").mtimeMs
          if sdt is odt then src_mod.splice (src_mod.indexOf outf), 1
      for inf in  src_mod
        await fse.copy "#{cfg.dir}/statiq/#inf", "#{cfg.out}/statiq/#inf"
      if cfg.watching
        cfg.statiq = src_files
        if prep then cfg.chok["#{cfg.dir}/statiq"] = { \statiq, '' }
      cb void 7
    catch e
      cb e, void
  else cb 'ERROR: no statiq file to copy' void

do-exec = (inf, outf, sel) !-->
  try
    r = switch sel
      | \ls   => fse.readFileSync inf, \utf-8 |> ls.compile
      | \pug  => pug.renderFile inf, cfg
      | \sass => (sass.compile inf, { style: \compressed }).css
    fse.writeFileSync outf, r
    console.log "#{new Date!toLocaleString!} => '#{sel}' compilation done"
  catch e
    console.log 'ERROR(doExec): Something went wrong!!\n\n'
    console.log e

final-cb = (e, r) !->
  if e?
    console.log "ERROR:\n(Results: #r)\n\n"
    console.log e

get-font = (cb) ->
  #
  # TODO
  #
  console.log 'loading the font'
  #
  cb void 18

launch-server = (cb) !->
  console.log 'launching dev server...'
  require! express
  app = express!
  app.use express.static \./dist
  app.listen 5001
  console.log 'dev server running on port 5001'



#
# TODO
#

task \wasm, '', !->
  #
  wasm-pack final-cb
  #

wasm-pack = (cb) !->
  #
  # TODO
  #
  require! {
    buffer
    child_process: { spawnSync }
  }
  #
  console.log 'wasm pack'
  #
  #process.chdir \wasm_test # change cwd
  #
  args = [
    \--log-level, \error, \build, \--dev, \--no-pack
    \--no-typescript, \--target, \no-modules
  ]
  opts =
    cwd: path.join process.cwd!, \wasm_test
  r = spawnSync \wasm-pack, args, opts
  stderr = r.stderr.toString \utf-8
  if /error/.test stderr
    #
    console.log '###### ERROR! #######'
    #
    console.log stderr
  else
    #
    console.log 'everything is fine'
    #
  #
  #
  cb void 76
  #

watching = (cb) !->
  require! chokidar
  chkstatiq = /statiq/
  watcher = chokidar.watch Object.keys cfg.chok, { awaitWriteFinish: yes }
  watcher.on \add, (pth) !->
    pth = pth.replaceAll '\\', '/'
    file = path.basename pth
    if chkstatiq.test pth and file not in cfg.statiq then copy-statiq final-cb
  watcher.on \change, (pth) !->
    pth = pth.replaceAll '\\', '/'
    if pth[0] is \. then pth.slice 2
    #
    # TODO: add font
    #
    if chkstatiq.test pth then copy-statiq final-cb
    else
      console.log "recompiling: '#pth'"
      do-exec pth, cfg.chok[pth].outf, cfg.chok[pth].lg
  watcher.on \error, (e) !->
    console.log 'CHOKIDAR ERROR:\n'
    console.log e
  watcher.on \unlink, (pth) !-> copy-statiq final-cb
  cb void 31

# TASKS ###############################

task \all, 'compile all active projects', (opts) !->
  check-env opts
  build-all!

task \clean, 'remove `dist`', (opts) !->
  check-env opts
  console.log "cleaning `#{cfg.dest}`..."
  fse.remove "./#{cfg.dest}", (e) ->
    if e? then console.log e
    else console.log "`#{cfg.dest}` removed successfully"

task \cmd, 'execute a command in a project', (opts) !->
  #
  # TODO: check project id
  #
  # TODO: check if command exists in this project
  #
  console.log 'not ready'
  #

task \compile, 'compile one project (USE PROJECT ID!)', (opts) !->
  opts.github = no
  opts.release = no
  check-env opts
  if opts.watch? then cfg.watching = opts.watch
  if opts.id?
    if opts.id > cfg.list.length
      console.log 'please select a project IN THE LIST!'
    else
      cfg.id = opts.id
      bldpack = building cfg.list[cfg.id]
      bld =
        if cfg.watching then [watching, launch-server] else []
        |> (bldpack ++)
        |> bach.series
      bld final-cb
  else console.log 'please set the id of the project you want to compile'

task \create, 'create a new project', (opts) !->
  console.log 'creating a new project...'
  require! {
    'readline-sync': readline,
    './core/project-src': { names, printout, srcs }
  }
  name = readline.question '# What is its name? > '
  pth = readline.question '# What is its path? > '
  console.log "name: #name, path: #pth"
  if readline.keyInYN '# Is it what you want?'
    console.log 'building the source...'
    cfg.out = pth
    gen = (sel, cb) !-->
      try
        out = srcs sel, name
        fse.writeFile "./#pth/#{names[sel]}", out
        cb void 32
      catch e
        console.log '=======> SOMETHING WENT WRONG <======='
        cb e, void
    (bach.series create-dir, (gen 'ls'), (gen 'pug'), (gen 'sass')) (e, _) !->
      if e? then console.log e
      else
        console.log 'creation DONE'
        console.log 'copy this in the config.ls to enable your project:'
        console.log printout name, pth
  else console.log 'canceling creation...'

task \list, 'list all projects actually available', (_) !->
  console.log 'Projects list:'
  for prj, i in cfg.list then console.log "#i => #{prj.name}"

task \serve, 'launch the server to get access to the projects', (opts) !->
  opts.github = no
  opts.release = no
  check-env opts
  #build-all!
  launch-server!
